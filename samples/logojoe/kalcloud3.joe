:args.
logo := !newInstance "Logo".
logo setTitle (args get 0).
dim := 124.
screenWidth := logo width.
screenHeight := logo height.
brx := screenWidth / 2.
bry := -1 * screenHeight / 2.

cloud := {:dim.
   bg := logo colorunder.
   red := (logo random 255 intValue).
   green := (logo random 255 intValue).
   blue := (logo random 255 intValue).
   logo penup ;
        forward (dim /2)
        left 90
        forward (dim / 2)
        right 90
        hideturtle.

   inc := 6.

   newComponent := {:c.
      delta := inc.
      !if ((logo random 2.0) > 1),{
          delta := delta * -1.
      }.
      c := c + delta.
      !if (c > 255),{
         c := 511 - c.
      },{
         !if (c < 1),{
           c := c * -1.
         }.
      }.
      c.
   }.
   forward := {:n.
      logo repeat n,{
         logo penup ;
              forward 1
              left 90
              forward 1.
         luc := logo colorunder.
         logo back 1
              right 90
              back 1
              pendown.

         !if (luc = bg not),{
             red := (luc getRed) + red / 2.
             green := (luc getGreen) + green / 2.
             blue := (luc getBlue) + blue / 2.
         }.

         red := newComponent exec red.
         green := newComponent exec green. 
         blue := newComponent exec blue.
   
         logo setpencolor red,green,blue
              forward 0
              penup ;
              forward 1
              pendown ;
              .
      }.
   }.

   s := 0.
   logo pendown ; forward 0.
   !while { s <= dim. },{
      forward exec s.
      logo left 90.
      forward exec s.
      logo left 90.
      forward exec s.
      logo left 90.
      forward exec s.
      logo penup ; forward 1 right 90 forward 1 right 180 pendown ;.
      s := s + 2.
   }.
}.

draw := {:size.
  cloud exec dim.
  logo penup ; home.
}.

cpTriangle := {:len.
   start := logo pos.
   rpt := len * 0.866 + 1 intValue.
   return := !newArray rpt.

   logo repeat rpt,{:repcount.
      here := logo pos.

      elem := !newArray (len + 1 intValue).
      logo penup.
      logo repeat ((len + 1) intValue),{:repcount1.
         elem set (repcount1 - 1),(logo colorunder).
         logo forward 1.
      }.
      return set (repcount - 1),elem.

      logo penup ;
           setxy (here get 0),(here get 1)
           left 90
           forward 1
           right 90
           forward 0.577
           pendown ;
      .
      len := len - 1.154.
   }.
   logo setxy (start get 0), (start get 1).
   return.
}.

cpRectangle := {:w,h.
   logo seth 0.
   return := !newArray w.

   logo repeat w,{:repcount.
      elem := !newArray h.
      logo penup.
      logo repeat h,{:repcount1.
         elem set (repcount1 - 1),(logo colorunder).
         logo forward 1.
      }.
      return set (repcount - 1),elem.

      logo penup ;
           back h
           left 90
           forward 1
           right 90
           pendown ;
      .
   }.
   return.
}.

showRectangle := {:rect.
   w := rect length.
   h := rect get 0 length.
   logo repeat w,{:repcount.
      elem := rect get (repcount - 1).
      logo repeat h,{:repcount1.
         logo setpencolor (elem get (repcount1 - 1))
         forward 1.
      }.
      logo penup ;
           back h
           left 90
           forward 1
           right 90
           pendown ;
      .
   }.
}.

showTriangle := {:treq,refl.
   start := logo pos.
   rpt := treq length.
   logo repeat rpt,{:repcount.
      here := logo pos.
      elem := treq get (repcount - 1).
      len := elem length.
      logo pendown.
      !if (refl = 0),{
         logo repeat len,{:repcount1.
            logo setpencolor (elem get (repcount1 - 1))
                 forward 1.
         }.
      },{
         logo repeat len,{:repcount1.
            logo setpencolor (elem get (len - repcount1))
                 forward 1.
         }.
      }.
      logo penup ;
           setxy (here get 0),(here get 1)
           left 90
           forward 1
           right 90
           forward 0.577
      .
   }.
   logo setxy (start get 0), (start get 1).
}.

showRotTriangle := {:treq,len,rot,refl.
   !switch rot
   case 0,{
      showTriangle exec treq,refl.
   }
   case 2,{
      logo penup ; forward len left 120.
      showTriangle exec treq,refl.
      logo penup ; right 120 back len.
   }
   case 1,{
      logo penup ; left 60 forward len left 180.
      showTriangle exec treq,refl.
      logo penup ; left 180 back len right 60.
   }.
}.
logo setbackground 0,0,0.

logo forever {
   logo setupdateoff.

   logo clearscreen ;
        setpencolor (logo random 256 intValue),
                    (logo random 256 intValue),
                    (logo random 256 intValue).

    draw exec (dim).

   triangle := cpTriangle exec dim.
   logo clearscreen ; penup; setxy brx,bry setpensize 2.

   cnt := 0.
   logo repeat 3, {:repcount.
      cnt := cnt + 1.
      showRotTriangle exec triangle,dim,(cnt % 3),0.
      logo forward dim.
   }.
   logo left 60 penup ; forward dim left 120 pendown.
   logo repeat 4, {:repcount.
      cnt := cnt + 1.
      showRotTriangle exec triangle,dim,(cnt % 3),1.
      logo forward dim.
   }.
   logo left 180 .
   logo repeat 4, {:repcount.
      cnt := cnt + 1.
      showRotTriangle exec triangle,dim,(cnt % 3),0.
      logo forward dim.
   }.
   logo left 120 penup ; forward dim left 60 pendown.

   logo repeat 4, {:repcount.
      cnt := cnt + 1.
      showRotTriangle exec triangle,dim,(cnt % 3),1.
      logo forward dim.
   }.

   modWidth := dim * 0.866 * 2 intValue.
   modHeight := dim * 3.

   logo setxy (brx - 1), (bry + 1).
   rectangle := cpRectangle exec modWidth,modHeight.
   logo clearscreen ; penup; setxy brx,bry setpensize 2 setbackground 0,0,0.

   horRep := screenWidth / modWidth + 1.
   verRep := screenHeight / modHeight + 1.
   logo repeat verRep, {
      logo repeat horRep, {
         showRectangle exec rectangle.
         logo showturtle.
      }.
      logo penup;
           left 90
           back (horRep * modWidth)
           right 90
           forward modHeight
      .
   }.
   logo fade 5.
}.
