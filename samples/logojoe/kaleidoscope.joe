:args.
logo := !newInstance "Logo".
logo setantialiasingoff ; setTitle (args get 0).
dim := 124.

star := {:size.
   seglen := size * 1.618.
   logo penup ;
        forward (size * 0.794)
        pendown ;
        right 162
        pendown ;
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        left 18
        penup ;
        back (size * 0.794)
   .
}.
circle := {:size.
   logo pendown ; arc 360, size penup.
}.

pargram := {:size, angle.
   logo penup ;
        forward (size / 2)
        right 90
        forward (size / 2)
        pendown ;
        right (angle)
        forward size
        right (180 - angle)
        forward size
        right (angle)
        forward size
        right (180 - angle)
        forward size
        penup ;
        back (size / 2)

        left 90
        back (size / 2)
    .
}.
   
square := {:size.
   pargram exec size,90.
}.

rhombus1 := {:size.
   logo left 110.
   pargram exec size,40.
   logo right 110.
}.
rhombus2 := {:size.
   logo left 20.
   pargram exec size,40.
   logo right 20.
}.

shapes := !array circle,square,rhombus1,rhombus2,star.

draw := {:size.
   logo setXORMode ; setpensize 4 repeat 8,{
      shape := logo random (shapes length) intValue.
      dist := logo random size.
      heading := logo random 60.
      logo setpencolor (logo random 255 intValue),
                       (logo random 255 intValue),
                       (logo random 255 intValue)
           left heading
           penup ;
           forward dist
           pendown.
           head2 := logo random 360.
           logo penup;
                forward dist
                left head2
                forward size
                pendown ;
                back (size * 2)
                penup ;
                forward size
                right head2
                back dist.
      shapes get shape exec (dist / 4).
      logo back dist
           right heading.
   }.
}.

cpTriangle := {:len.
   start := logo pos.
   rpt := len * 0.866 + 1 intValue.
   return := !newArray rpt.

   logo repeat rpt,{:repcount.
      here := logo pos.

      elem := !newArray (len + 1 intValue).
      logo penup.
      logo repeat ((len + 1) intValue),{:repcount1.
         elem set (repcount1 - 1),(logo colorunder).
         logo forward 1.
      }.
      return set (repcount - 1),elem.

      logo penup ;
           setxy (here get 0),(here get 1)
           left 90
           forward 1
           right 90
           forward 0.577
           pendown ;
      .
      len := len - 1.154.
   }.
   logo setxy (start get 0), (start get 1).
   return.
}.

showTriangle := {:treq,refl.
   start := logo pos.
   rpt := treq length.
   logo repeat rpt,{:repcount.
      here := logo pos.
      elem := treq get (repcount - 1).
      len := elem length.
      logo pendown.
      !if (refl = 0),{
         logo repeat len,{:repcount1.
            logo setpencolor (elem get (repcount1 - 1))
                 forward 1.
         }.
      },{
         logo repeat len,{:repcount1.
            logo setpencolor (elem get (len - repcount1))
                 forward 1.
         }.
      }.
      logo penup ;
           setxy (here get 0),(here get 1)
           left 90
           forward 1
           right 90
           forward 0.577
      .
   }.
   logo setxy (start get 0), (start get 1).
}.

showRotTriangle := {:treq,len,rot,refl.
   !switch rot
   case 0,{
      showTriangle exec treq,refl.
   }
   case 2,{
      logo penup ; forward len left 120.
      showTriangle exec treq,refl.
      logo penup ; right 120 back len.
   }
   case 1,{
      logo penup ; left 60 forward len left 180.
      showTriangle exec treq,refl.
      logo penup ; left 180 back len right 60.
   }.
}.

logo forever {
   logo clearscreen ; setbackground 0,0,0.
   draw exec (dim).

   triangle := cpTriangle exec dim.
   logo clearscreen ; setbackground 0,0,0 penup; setxy 320,-240.
   cnt := 2.
   logo repeat 3,{
      cnt := 0.
      logo repeat 4, {:repcount.
         cnt := cnt + 1.
         showRotTriangle exec triangle,dim,(cnt % 3),0.
         logo forward dim.
      }.
      logo left 60 penup ; forward dim left 120 pendown.
      cnt := cnt + 1.
      logo repeat 5, {:repcount.
         cnt := cnt + 1.
         showRotTriangle exec triangle,dim,(cnt % 3),1.
         logo forward dim.
      }.
      logo left 180 .
      logo repeat 5, {:repcount.
         cnt := cnt + 1.
         showRotTriangle exec triangle,dim,(cnt % 3),0.
         logo forward dim.
      }.
      logo left 120 penup ; forward dim left 60 pendown.
      cnt := cnt + 1.

      logo repeat 4, {:repcount.
         cnt := cnt + 1.
         showRotTriangle exec triangle,dim,(cnt % 3),1.
         logo forward dim.
      }.
      logo left 180 .
   }.

   logo setDelay 5000 forward 0 setDelay 0.
}.
