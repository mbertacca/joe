#! /usr/bin/java com.veryant.joe.JavaObjectsExecutor

:args.
logo := !newInstance "Logo".
logo setTitle (args get 0).

iteration := 6.

midPoints := !newInstance "java.util.HashMap".

getMidPoint := {:ax,ay,bx,by.
   key := "" + ax + ";" + ay + ";" + bx + ";" + by.
   return := midPoints get key.
   !if (!isNull return),{
      distance:=logo sqrt ((logo power (bx - ax),2) + (logo power (by - ay),2)).
      return := !array ((ax+bx)/2),(((ay+by)/2)+(distance*(logo random 0.2))).
      midPoints put key,(!array (return get 0),(return get 1)).
   }.
   return.
}.

triangle := {:ax,ay,bx,by,cx,cy,it.

   !if (it < iteration),{
      m := getMidPoint exec ax,ay,bx,by;
      mx1 := m get 0.
      my1 := m get 1.

      m := getMidPoint exec bx,by,cx,cy;
      mx2 := m get 0.
      my2 := m get 1.

      m := getMidPoint exec cx,cy,ax,ay;
      mx3 := m get 0.
      my3 := m get 1.

      it := it + 1.
      triangle exec ax,ay,mx1,my1,mx3,my3,it.
      triangle exec bx,by,mx2,my2,mx1,my1,it.
      triangle exec cx,cy,mx3,my3,mx2,my2,it.
      triangle exec mx1,my1,mx3,my3,mx2,my2,it.
      .
   },{
      logo penup ;
           setxy ax,ay
           pendown ;
           setxy bx,by
           setxy cx,cy
           setxy ax,ay
      .
   }.
}.

logo forever {
   triangle exec -300,-220,
                 0, 220,
               300,-220,
               0.
   midPoints clear.
   logo setDelay 5000 forward 0 clearscreen ; setDelay 0.
}.
