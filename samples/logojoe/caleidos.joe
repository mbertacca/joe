:args.
logo := !newInstance "Logo",800,600.
logo setDelay 0 setTitle (args get 0).

dim := 256.
pensize := dim / 32.

circle := {:size.
   logo pendown ; arc 360, size penup.
}.

pargram := {:size, angle.
   logo penup ;
        forward (size / 2)
        right 90
        forward (size / 2)
        pendown ;
        right (angle)
        forward size
        right (180 - angle)
        forward size
        right (angle)
        forward size
        right (180 - angle)
        forward size
        penup ;
        back (size / 2)

        left 90
        back (size / 2)
    .
}.
   
square := {:size.
   pargram exec size,90.
}.

rhombus1 := {:size.
   logo left 110.
   pargram exec size,40.
   logo right 110.
}.
rhombus2 := {:size.
   logo left 20.
   pargram exec size,40.
   logo right 20.
}.

pentagon := {:size.
   seglen := size * 1.618.
   logo penup ;
        forward (size * 0.794)
        pendown ;
        right 126
        forward size
        right 72
        forward size
        right 72
        forward size
        right 72
        forward size
        right 72
        forward size
        penup ;
        left 54
        back (size * 0.794)
}.
star := {:size.
   seglen := size * 1.618.
   logo penup ;
        forward (size * 0.794)
        pendown ;
        right 162
        pendown ;
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        left 18
        penup ;
        back (size * 0.794)
   .
}.

shapes := !array circle,square,rhombus1,rhombus2,pentagon,star.

draw1 := {:size.
   simm := 2 + (logo random 11 intValue).
   angle := 180.0 / simm.
   logo repeat (64 / simm),{
      shape := logo random (shapes length) intValue.
      dist := logo random size.
      heading := logo random angle intValue.
      logo seth heading
           setpencolor (logo random 255 intValue),
                       (logo random 255 intValue),
                       (logo random 255 intValue).
      logo repeat simm,{
         logo penup ; forward dist .
         shapes get shape exec (dist / 6).
         logo penup ;
            back dist
            right heading.
         logo penup ; forward dist .
         shapes get shape exec (dist / 6)
            back dist
            right ((angle * 2) - heading).
      }.
      logo left heading penup.
   }.
}.

draw2 := {:size.
   simm := 2 + (logo random 11 intValue).
   angle := 360.0 / simm.
   logo repeat (64 / simm),{
      shape := logo random (shapes length) intValue.
      dist := logo random size.
      heading := logo random angle intValue.
      logo setpencolor (logo random 255 intValue),
                       (logo random 255 intValue),
                       (logo random 255 intValue).
      logo repeat simm,{
         logo left heading
              penup ; forward dist .
         shapes get shape exec (dist / 6).
         logo penup ; back dist 
              right (heading * 2)
              penup ; forward dist.
         shapes get shape exec (dist / 6).
         logo penup ; back dist.

         logo right (angle - heading) penup.
      }.
   }.
}.

draws := !array draw1,draw2.

logo forever {
   logo setbackground 0,0,0
        setpensize pensize setXORMode.
   draws get (logo random 2 intValue) exec dim.
   logo setDelay 5000 forward 0 setDelay 0.
}.
