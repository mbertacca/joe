#! /usr/bin/java com.veryant.joe.JavaObjectsExecutor

:args.
logo := !newInstance "Logo".
logo setTitle (args get 0).

empty := logo colorunder.

step := 20.
startx := -8 * step.
starty := -8 * step.
endx :=  8 * step.
endy :=  8 * step.

logo setDelay 0.
logo setpensize (step / 2).

logo penup ;
     setxy startx,starty
     pendown ;
     forward (endy - starty)
     right 90
     forward (endx - startx - (step + (step / 2)))
     left 90
     forward step
     right 90
     forward step
     right 90
     forward step
     left 90
     forward (step / 2)
     right 90
     forward (endy - starty)
     right 90
     forward (endx - startx)
     right 90
.

x := startx + step.
y := starty.

logo penup ;
     setxy x,y
     pendown ;
     forward step
.

comb := !array 
   (!array 0,1,2,3),
   (!array 1,0,2,3),
   (!array 0,2,1,3),
   (!array 1,2,0,3),
   (!array 2,0,1,3),
   (!array 2,1,0,3),

   (!array 0,1,3,2),
   (!array 1,0,3,2),
   (!array 0,2,3,1),
   (!array 1,2,3,0),
   (!array 2,0,3,1),
   (!array 2,1,3,0),

   (!array 0,3,1,2),
   (!array 1,3,0,2),
   (!array 0,3,2,1),
   (!array 1,3,2,0),
   (!array 2,3,0,1),
   (!array 2,3,1,0),

   (!array 3,0,1,2),
   (!array 3,1,0,2),
   (!array 3,0,2,1),
   (!array 3,1,2,0),
   (!array 3,2,0,1),
   (!array 3,2,1,0).

   
go := {
   ord := comb get ((logo random 24) intValue).
   i := -1.
   !while {i := i + 1. i < 4 },{
      logo seth ((ord get i) * 90)
           penup ;
           forward step.
      under := logo colorunder.
      logo back step
           pendown.
      !if (under = empty), {
         logo forward step.
         go exec.
         logo seth ((ord get i) * 90) back step.
      }.
   }.
}.

go exec.

solve := {solve:dir.
   here := logo pos.
   !if (((here get 0) = exitx) and ((here get 1) = exity)),{
       null.
       !break "solve".
   }.
   i := -90.
   !while {i := i + 90. i < 360 },{wnext:.
      !if (i = dir),{
         !break "wnext".
      }.
      logo seth i
           penup ;
           forward (step / 2).
      under := logo colorunder.
      logo back (step / 2)
           pendown.
      !if (under = empty not), {
         logo forward step.
         exit := solve exec ((i + 180) % 360).
         !if (!isNull exit),{
             here := null.
             !breakLoop.
         },{
             logo setpencolor 128,0,0
                  seth i back step
                  setpencolor 0,128,0.
         }.
      }.
   }.
   here.
}.
exitx := endx - step.
exity := endy.
logo setxy x,y
     setDelay 5
     setpencolor 0,128,0.
solve exec 180.
