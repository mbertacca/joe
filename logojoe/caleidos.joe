:args.
logo := !newInstance "Logo",800,600.
logo setDelay 0 setXORMode ; setTitle (args get 0).

dim := 256.
pensize := dim / 32.

circle := {:size.
   logo pendown ; arc 360, size penup.
}.
   
pargram := {:size, angle.
   logo penup ;
        forward (size / 2)
        right 90
        pendown ;
        forward (size / 2)
        right (angle)
        forward size
        right (180 - angle)
        forward size
        right (angle)
        forward size
        right (180 - angle)
        forward (size / 2)

        penup ;
        left 90
        back (size / 2)
    .
}.

square := {:size.
   pargram exec size,90.
}.

rombus := {:size.
   pargram exec size,45.
}.

pentagon := {:size.
   seglen := size * 1.618.
   logo penup ;
        forward (size * 0.794)
        pendown ;
        right 126
        forward size
        right 72
        forward size
        right 72
        forward size
        right 72
        forward size
        right 72
        forward size
        penup ;
        left 54
        back (size * 0.794)
}.
star := {:size.
   seglen := size * 1.618.
   logo penup ;
        forward (size * 0.794)
        pendown ;
        right 162
        pendown ;
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        right 144
        forward seglen
        left 18
        penup ;
        back (size * 0.794)
   .
}.

shapes := !array circle,square,rombus,pentagon,star.

draw := {:size.
   simm := 3 + (logo random 4 intValue).
   angle := 180 / simm.
   logo repeat 16,{
      shape := logo random (shapes length) intValue.
      dist := logo random size.
      heading := logo random angle.
      logo seth heading
           setpencolor (logo random 255 intValue),
                       (logo random 255 intValue),
                       (logo random 255 intValue).
      logo repeat simm,{
         logo penup ; forward dist .
         shapes get shape exec (dist / 6).
         logo penup ;
            back dist
            right (heading).
         logo penup ; forward dist .
         shapes get shape exec (dist / 6)
            back dist
            right ((angle * 2) - heading).
      }.
      logo left heading penup.
   }.
}.

logo repeat 3, {
   logo setbackground 0,0,0
        setpensize pensize setXORMode.
   draw exec dim.
   logo setDelay 3000 forward 0 setDelay 0.
}
